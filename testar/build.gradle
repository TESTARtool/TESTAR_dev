apply plugin: 'application'

sourceSets {
    main {
        resources {
            srcDirs = ['resources']
            excludes = ['settings/']
        }
    }

    oracle {
        java {
            srcDirs =['resources/settings']
        }

        compileClasspath += main.output
        compileClasspath += main.compileClasspath
    }
}

/**
 * Task to run TESTAR using gradle
 * This task set the working directory to the install folder and runs testar.bat
 */
task runTestar(type: Exec) {
    // Make sure temp dir exists on case-sensitive OSs
    doFirst{
        new File("${project.buildDir}/install/testar/bin/output/temp").mkdirs()
    }

    group = 'custom_testar'
    description ='runTESTAR'
    workingDir 'target/install/testar/bin'
    commandLine 'cmd', '/c', 'testar.bat'

    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
		environment "JAVA_TOOL_OPTIONS", "-Djava.awt.headless=false"
		commandLine "./testar"
	} else if (System.getProperty("os.name").toLowerCase().contains("linux")) {
		commandLine "./testar"
	} else {
		commandLine 'cmd', '/c', 'testar.bat'
	}
}

/*  There's error on Linux, crashing the JVM on unregistering the NativeHook
    For now, use the old version, 2.1.1 should fix the issue
    https://github.com/kwhat/jnativehook/issues/168
 */
def jnativehook = System.getProperty("os.name").toLowerCase() == "linux"
        ? 'com.1stleg:jnativehook:2.0.2'
        : 'com.1stleg:jnativehook:2.1.0'

task customZipDistribution(type: Zip) {
	doFirst{
		new File("${project.buildDir}/install/testar/bin/settings","desktop_generic.sse").text=""
	}
    group = 'custom_testar'
    description ='customZipDistribution'
    from 'target/install/testar'
    include '*'
   	include '*/**' //include subdirectories

   	archiveName 'testarDistribution.zip'
   	destinationDir(file('target/install'))
}

dependencies {
    compile project(':core')
    compile project(':native')
    compile project(':graphdb')
    compile files('./lib/jsyntaxpane-1.1.5.jar')
    compile files('./lib/sikulixapi.jar')
    compile files('./lib/eye2.jar')
    compile files('./lib/gson-2.8.5.jar')
    compile jnativehook
    compile 'com.google.guava:guava:26.0-jre'
    compile group: 'com.jgoodies', name: 'jgoodies-common', version: '1.8.1'
    compile group: 'com.jgoodies', name: 'jgoodies-forms', version: '1.9.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.16.v20190411'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.16.v20190411'
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.4.16.v20190411'
    compile group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '9.4.16.v20190411'
    compile group: 'org.eclipse.jetty', name: 'apache-jsp', version: '9.4.16.v20190411'
    compile group: 'org.eclipse.jetty', name: 'apache-jstl', version: '9.4.16.v20190411'
    runtime project(':windows')
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.2'
}
evaluationDependsOn(':windows')

task prepareSettings(type: Copy) {
    from 'resources/settings'
    into 'target/scripts/settings'
}

task prepareSUTs(type: Copy) {
    from 'distributionSUTs'
    into 'target/scripts/suts'
}

task prepareOracles(type: Copy) {
    from 'target/classes/java/oracle'
    into 'target/scripts/settings'
}

task prepareOutputFolder(type: Copy) {
    from 'resources/output'
    into 'target/scripts/output'
}

/**
 * Copy windows.dll from the subproject windows into target scripts
 * so it can be used with the creation of the distribution.
 */
task prepareWindowsDLL(type: Copy) {
    from '../windows/target/resources/main'
    into 'target/scripts'
    include 'windows.dll'
}

applicationDistribution.from("../webdriver/resources/web-extension") {
    into "web-extension"
}

prepareOracles.dependsOn(oracleClasses)

/**
 * Set the arguments of the jvm in the start scripts.
 * To debug build TESTAR with the -DDEBUG=true.
 */
startScripts {
    if(System.getProperty('DEBUG', 'false') ) {
        applicationDefaultJvmArgs = ["-Dlogback.configurationFile=logback.xml",
                                     '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005']
    } else {
        applicationDefaultJvmArgs = ["-Dlogback.configurationFile=logback.xml"]
    }
    
    doLast {
        def winScriptFile  = file getWindowsScript()
        def winFileText = winScriptFile.text
        winFileText = winFileText.replaceAll('set CLASSPATH=.*', 'rem original CLASSPATH declaration replaced by:\nset CLASSPATH=%APP_HOME%\\\\lib\\\\\\*')
        winScriptFile.text = winFileText
    }
}

startScripts.dependsOn {
    tasks.findAll { task -> task.name.startsWith('prepare')}
}

//running testar requires the distribution to be installed.
runTestar.dependsOn(installDist)
customZipDistribution.dependsOn(installDist)

mainClassName='org.fruit.monkey.Main'
